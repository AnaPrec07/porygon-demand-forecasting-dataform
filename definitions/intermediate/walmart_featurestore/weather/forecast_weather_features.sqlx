
config {
  type: "table",
  schema: "walmart_featurestore",
  description: "Rolling weather forecast features per state.",
  tags: ["intermediate", "walmart", "features"],
  assertions: {
    nonNull: [
      "ctx_date_month",
      "ctx_state_id",
    ],
    uniqueKey: [
      "ctx_date_month",
      "ctx_state_id",
    ],
  }
}

SELECT 
  ctx_date_month, 
  ctx_state_id,

  -- average values forecast (3 months forward)
  AVG(mean_temperature) OVER w AS feature_forecast_avg_temperature_3m, 
  AVG(mean_dewp) OVER w AS feature_forecast_avg_dewp_3m, 
  AVG(mean_visib) OVER w AS feature_forecast_avg_visib_3m, 
  AVG(mean_wdsp) OVER w AS feature_forecast_avg_wdsp_3m, 
  AVG(mean_prcp) OVER w AS feature_forecast_avg_prcp_3m, 
  AVG(mean_sndp) OVER w AS feature_forecast_avg_sndp_3m, 
  AVG(mean_gust) OVER w AS feature_forecast_avg_gust_3m, 
  AVG(mean_fog) OVER w AS feature_forecast_avg_fog_3m, 
  AVG(mean_tornado_funnel_cloud) OVER w AS feature_forecast_avg_tornado_funnel_cloud_3m, 
  AVG(mean_thunder) OVER w AS feature_forecast_avg_thunder_3m, 
  AVG(mean_snow_ice_pellets) OVER w AS feature_forecast_avg_snow_ice_pellets_3m, 
  AVG(mean_rain_drizzle) OVER w AS feature_forecast_avg_rain_drizzle_3m, 

  -- min values forecast (3 months forward)
  MIN(min_temperature) OVER w AS feature_forecast_min_temperature_3m,
  MIN(min_dewp) OVER w AS feature_forecast_min_dewp_3m,
  MIN(min_visib) OVER w AS feature_forecast_min_visib_3m,
  MIN(min_wdsp) OVER w AS feature_forecast_min_wdsp_3m,
  MIN(min_prcp) OVER w AS feature_forecast_min_prcp_3m,
  MIN(min_sndp) OVER w AS feature_forecast_min_sndp_3m,
  MIN(min_gust) OVER w AS feature_forecast_min_gust_3m,
  MIN(min_fog) OVER w AS feature_forecast_min_fog_3m,
  MIN(min_tornado_funnel_cloud) OVER w AS feature_forecast_min_tornado_funnel_cloud_3m,
  MIN(min_thunder) OVER w AS feature_forecast_min_thunder_3m,
  MIN(min_snow_ice_pellets) OVER w AS feature_forecast_min_snow_ice_pellets_3m,
  MIN(min_rain_drizzle) OVER w AS feature_forecast_min_rain_drizzle_3m,

  -- max values forecast (3 months forward)
  MAX(max_temperature) OVER w AS feature_forecast_max_temperature_3m,
  MAX(max_dewp) OVER w AS feature_forecast_max_dewp_3m,
  MAX(max_visib) OVER w AS feature_forecast_max_visib_3m,
  MAX(max_wdsp) OVER w AS feature_forecast_max_wdsp_3m,
  MAX(max_prcp) OVER w AS feature_forecast_max_prcp_3m,
  MAX(max_sndp) OVER w AS feature_forecast_max_sndp_3m,
  MAX(max_gust) OVER w AS feature_forecast_max_gust_3m,
  MAX(max_fog) OVER w AS feature_forecast_max_fog_3m,
  MAX(max_tornado_funnel_cloud) OVER w AS feature_forecast_max_tornado_funnel_cloud_3m,
  MAX(max_thunder) OVER w AS feature_forecast_max_thunder_3m,
  MAX(max_snow_ice_pellets) OVER w AS feature_forecast_max_snow_ice_pellets_3m,
  MAX(max_rain_drizzle) OVER w AS feature_forecast_max_rain_drizzle_3m

FROM ${ref("weather_features")}

WINDOW w AS (
  PARTITION BY ctx_state_id
  ORDER BY ctx_date_month
  ROWS BETWEEN 1 FOLLOWING AND 3 FOLLOWING
)
